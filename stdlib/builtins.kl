#lang kernel

-- Allow e.g. unit instead of (unit)

[import [shift kernel 1]]

(meta
  (define optional-parens
    (lambda (symbol)
      (lambda (stx)
        (case (open-syntax stx)
          [(identifier-contents x)
           (pure (((close-syntax stx) stx) (list-contents (:: symbol (nil)))))]
          [(list-contents (:: _ args))
           (pure (((close-syntax stx) stx) (list-contents (:: symbol args))))])))))

-- TODO: wrap 'datatype' so that parentheses are also optional for
-- the nullary types and constructors it creates
(define-macros
  ([my-Unit (optional-parens 'Unit)]
   [my-unit (optional-parens 'unit)]
   [my-Bool (optional-parens 'Bool)]
   [my-true (optional-parens 'true)]
   [my-false (optional-parens 'false)]
   [my-nothing (optional-parens 'nothing)]))

(export (rename ([my-Unit Unit] [my-unit unit]
                 [my-Bool Bool] [my-true true] [my-false false]
                 [my-nothing nothing])
                my-Unit my-unit
                my-Bool my-true my-false
                Maybe my-nothing just))
